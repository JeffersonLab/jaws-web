plugins {
    id 'war'
}

description 'Jefferson Lab Alarm Warning System (JAWS) web admin interface'
group 'org.jlab'
version '4.3.0'

// IndexDB Client-side cache data version
ext.cacheVersion = '1'

tasks.withType(JavaCompile) {
    options.release = 11
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

repositories {
    mavenCentral()

    maven {
        url "https://packages.confluent.io/maven"
    }

    maven {
        url "https://s01.oss.sonatype.org/content/repositories/releases/"
    }
}

dependencies {

    // No matter what smoothness weblib MUST be included in WAR (https://github.com/JeffersonLab/smoothness/issues/4)
    implementation 'org.jlab:smoothness-weblib:4.3.0'

    // Smoothness setup should install these into Wildfly
    providedCompile 'org.tuckey:urlrewritefilter:4.0.4',
                    'org.jlab:jaws-libj:4.6.0',
                    'io.confluent:kafka-avro-serializer:7.4.0'

    // Wildfly 26.1.3.Final provides:
    providedCompile 'javax:javaee-api:8.0.1',
                    'javax.servlet:jstl:1.2',
                    'com.fasterxml.jackson.core:jackson-databind:2.12.7.1',
                    'org.apache.avro:avro:1.10.2',
                    'com.google.guava:guava:31.1-jre',
                    'com.google.guava:failureaccess:1.0.1',
                    'org.slf4j:slf4j-api:1.7.36'
}

war {
    archiveFileName = 'jaws.war'
    filesMatching('WEB-INF/web.xml') {
        filter {
            String line -> line.replaceAll("@VERSION@", project.version)
        }
    }

    filesMatching('worker.mjs') {
        filter {
            String line -> line.replaceAll("@VERSION@", project.version)
        }
    }

    filesMatching('resources/modules/jaws-admin-gui/db.mjs') {
        filter {
            String line -> line.replaceAll("@CACHE_VERSION@", cacheVersion)
        }
    }
}